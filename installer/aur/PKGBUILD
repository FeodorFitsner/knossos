# Maintainer: kteam <knossos-team at mpimf-heidelberg.mpg.de>
#
# KNOSSOS saves its user preferences in $HOME/.config/MPIMF/

pkgname=knossos
pkgver=4.1
pkgrel=2
arch=('x86_64')
pkgdesc="A software tool for the visualization and annotation of 3D image data. It was developed for the rapid reconstruction of neural morphology and connectivity."
url="http://www.knossostool.org/"
license=("GPL2")
depends=("boost"
    "curl"
    "freeglut"
    "glu"
    "glut"
    "libgl"
    "qt5-base"
    "qt5-python27"
    "quazip-qt5"
    "snappy"
)
makedepends=("cmake"
    "ninja"
)
source=("https://github.com/knossos-project/knossos/archive/v${pkgver}.tar.gz"
    "knossos.desktop"
)
sha256sums=('007555d603a6775b102cf2798580026029bece642b856f4d4e98236d99f3d154'
            '462aa5f01abf5f330eeca964789513de7061c6c801ee779245f864b14b583012')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    cmake -G Ninja .
    ninja
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    install -Dm755 knossos "$pkgdir/usr/bin/knossos"
    install -Dm644 resources/icons/logo.ico "$pkgdir/usr/share/pixmaps/knossos.ico"
    install -Dm644 ../knossos.desktop "$pkgdir/usr/share/applications/knossos.desktop"
}
